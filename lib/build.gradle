plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id("maven-publish")
}

android {
    namespace 'com.dede.blurhash_android'
    compileSdk 33

    defaultConfig {
        minSdk 21
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        buildConfig = false
    }
}

version = "1.0.0"
group = "io.github.hushenghao"
String artifactName = "blurhash-android"

dependencies {
    implementation 'androidx.core:core-ktx:1.7.0'

    compileOnly 'io.coil-kt:coil:2.2.2'
    compileOnly 'com.github.bumptech.glide:glide:4.15.0'
    compileOnly 'com.facebook.fresco:fresco:3.0.0'
    compileOnly 'com.squareup.picasso:picasso:2.8'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

Properties localProps = new Properties()
File localProperties = new File(rootProject.rootDir, "local.properties")
if (localProperties.exists() && localProperties.isFile()) {
    localProperties.withInputStream { localProps.load(it) }
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name = "gitHubPackages"
                url = uri("https://maven.pkg.github.com/hushenghao/blurhash-android")
                credentials {
                    username = localProps.getProperty("githubActor")
                    password = localProps.getProperty("githubToken")
                }
            }
            maven {
                name = "sonatypeStaging"
                url = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2")
                credentials {
                    username = localProps.getProperty("ossrhUsername")
                    password = localProps.getProperty("ossrhPassword")
                }
            }
            maven {
                name = "sonatypeSnapshot"
                url = uri("https://s01.oss.sonatype.org/content/repositories/snapshots")
                credentials {
                    username = localProps.getProperty("ossrhUsername")
                    password = localProps.getProperty("ossrhPassword")
                }
            }
        }
        publications {
            release(MavenPublication) {
                from components.release
                artifact androidSourcesJar
                artifactId = artifactName

                pom {
                    packaging 'aar'
                    name = 'Blurhash Android'
                    description = 'Kotlin extensions of Blurhash for ImageView'
                    url = 'https://github.com/hushenghao/blurhash-android'

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }

                    developers {
                        developer {
                            id = 'hushenghao'
                            name = 'Hu Shenghao'
                            email = 'dede.hu@qq.com'
                        }
                    }

                    scm {
                        url = 'https://github.com/hushenghao/blurhash-android'
                    }
                }
            }
        }
    }
}